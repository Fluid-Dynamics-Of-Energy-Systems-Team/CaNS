module mod_mom
  use mpi
  use mod_param     , only: dims, ktot, imax, jmax
  use mod_common_mpi, only: ierr
  use mod_common_cuda, only: stream1, stream2
  use cudafor
  implicit none
  private
  public momxad,momyad,momzad,momxp,momyp,momzp
  contains
  subroutine momxad(nx,ny,nz,dxi,dyi,dzi,dzci_d,dzfi_d,dzflzi_d,visc,u_d,v_d,w_d,dudt_d,taux)
    implicit none
    integer, intent(in) :: nx,ny,nz
    real(8), intent(in) :: dxi,dyi,dzi,visc
    real(8), intent(in), dimension(0:), device :: dzci_d,dzfi_d
    real(8), intent(in), dimension(0:), device :: dzflzi_d
    real(8), dimension(0:,0:,0:), intent(in), device :: u_d,v_d,w_d
    real(8), dimension(:,:,:), intent(out), device :: dudt_d
    real(8), dimension(3)  , intent(out) :: taux
    real(8), dimension(3), device :: taux_d
    integer :: im,ip,jm,jp,km,kp,i,j,k
    real(8) :: uuip,uuim,uvjp,uvjm,uwkp,uwkm
    real(8) :: dudxp,dudxm,dudyp,dudym,dudzp,dudzm
    integer :: nxg,nyg,nzg
    integer :: istat
    istat = cudaDeviceSynchronize()
    !
    !$OMP PARALLEL DO DEFAULT(none) &
    !$OMP PRIVATE(i,j,k,im,jm,km,ip,jp,kp) &
    !$OMP PRIVATE(uuip,uuim,uvjp,uvjm,uwkp,uwkm) &
    !$OMP PRIVATE(dudxp,dudxm,dudyp,dudym,dudzp,dudzm) &
    !$OMP SHARED(nx,ny,nz,dxi,dyi,dzi,visc,u,v,w,dudt,dzci,dzfi)
   
    !$cuf kernel do(3) <<<*,*>>>
    do k=1,nz
      kp = k + 1
      km = k - 1
      do j=1,ny
        jp = j + 1
        jm = j - 1
        do i=1,nx
          ip = i + 1
          im = i - 1
          uuip  = 0.25d0*( u_d(ip,j,k)+u_d(i,j,k) )*( u_d(ip,j ,k )+u_d(i,j ,k ) )
          uuim  = 0.25d0*( u_d(im,j,k)+u_d(i,j,k) )*( u_d(im,j ,k )+u_d(i,j ,k ) )
          uvjp  = 0.25d0*( u_d(i,jp,k)+u_d(i,j,k) )*( v_d(ip,j ,k )+v_d(i,j ,k ) )
          uvjm  = 0.25d0*( u_d(i,jm,k)+u_d(i,j,k) )*( v_d(ip,jm,k )+v_d(i,jm,k ) )
          uwkp  = 0.25d0*( u_d(i,j,kp)+u_d(i,j,k) )*( w_d(ip,j ,k )+w_d(i,j ,k ) )
          uwkm  = 0.25d0*( u_d(i,j,km)+u_d(i,j,k) )*( w_d(ip,j ,km)+w_d(i,j ,km) )
          dudxp = (u_d(ip,j,k)-u_d(i,j,k))*dxi
          dudxm = (u_d(i,j,k)-u_d(im,j,k))*dxi
          dudyp = (u_d(i,jp,k)-u_d(i,j,k))*dyi
          dudym = (u_d(i,j,k)-u_d(i,jm,k))*dyi
          dudzp = (u_d(i,j,kp)-u_d(i,j,k))*dzci_d(k)
          dudzm = (u_d(i,j,k)-u_d(i,j,km))*dzci_d(km)
          !
          ! Momentum balance
          !
          dudt_d(i,j,k) = dxi*(     -uuip + uuim ) + (dudxp-dudxm)*visc*dxi + &
                        dyi*(     -uvjp + uvjm ) + (dudyp-dudym)*visc*dyi + &
                        dzfi_d(k)*( -uwkp + uwkm ) + (dudzp-dudzm)*visc*dzfi_d(k)
        enddo
      enddo
    enddo

    istat = cudaDeviceSynchronize()

    ! TODO: init taux_d
    !$OMP END PARALLEL DO
    taux(:) = 0.
    !$cuf kernel do(2) <<<*,*>>>
    do k=1,nz
      do i=1,nx
        dudyp = (u_d(i,1 ,k)-u_d(i,0   ,k))*dyi*visc*dzflzi_d(k)
        dudym = (u_d(i,ny,k)-u_d(i,ny+1,k))*dyi*visc*dzflzi_d(k)
        taux_d(2) = taux_d(2) + (dudyp+dudym)
      enddo
    enddo
    !$cuf kernel do(2) <<<*,*>>>
    do j=1,ny
      do i=1,nx
        dudzp = (u_d(i,j,1 )-u_d(i,j,0   ))*dzci_d(0)*visc
        dudzm = (u_d(i,j,nz)-u_d(i,j,nz+1))*dzci_d(nz)*visc
        taux_d(3) = taux_d(3) + (dudzp+dudzm)
      enddo
    enddo
    taux = taux_d 
    istat = cudaDeviceSynchronize()
   
    call mpi_allreduce(MPI_IN_PLACE,taux(1),3,MPI_REAL8,MPI_SUM,MPI_COMM_WORLD,ierr)
    nxg = nx*dims(1)
    nyg = ny*dims(2)
    nzg = nz
    taux(1) = taux(1)/(1.d0*nyg)
    taux(2) = taux(2)/(1.d0*nxg)
    taux(3) = taux(3)/(1.d0*nxg*nyg)
    return
  end subroutine momxad
  !
  subroutine momyad(nx,ny,nz,dxi,dyi,dzi,dzci_d,dzfi_d,dzflzi_d,visc,u_d,v_d,w_d,dvdt_d,tauy)
    implicit none
    integer, intent(in) :: nx,ny,nz
    real(8), intent(in) :: dxi,dyi,dzi,visc
    real(8), intent(in), dimension(0:), device :: dzci_d,dzfi_d
    real(8), intent(in), dimension(0:), device :: dzflzi_d
    real(8), dimension(0:,0:,0:), intent(in), device :: u_d,v_d,w_d
    real(8), dimension(:,:,:), intent(out), device :: dvdt_d
    real(8), dimension(3), intent(out) :: tauy
    real(8), dimension(3), device :: tauy_d
    integer :: im,ip,jm,jp,km,kp,i,j,k
    real(8) :: uvip,uvim,vvjp,vvjm,wvkp,wvkm
    real(8) :: dvdxp,dvdxm,dvdyp,dvdym,dvdzp,dvdzm
    integer :: nxg,nyg,nzg
    integer :: istat

    istat = cudaStreamCreate(stream1)
    !
    !$OMP PARALLEL DO DEFAULT(none) &
    !$OMP PRIVATE(i,j,k,im,jm,km,ip,jp,kp) &
    !$OMP PRIVATE(uvip,uvim,vvjp,vvjm,wvkp,wvkm) &
    !$OMP PRIVATE(dvdxp,dvdxm,dvdyp,dvdym,dvdzp,dvdzm) &
    !$OMP SHARED(nx,ny,nz,dxi,dyi,dzi,visc,u,v,w,dvdt,dzci,dzfi)
    !$cuf kernel do(3) <<<*,*,stream=stream1>>>
    do k=1,nz
      kp = k + 1
      km = k - 1
      do j=1,ny
        jp = j + 1
        jm = j - 1
        do i=1,nx
          ip = i + 1
          im = i - 1
          uvip  = 0.25d0*( u_d(i ,j,k)+u_d(i ,jp,k) )*( v_d(i,j,k )+v_d(ip,j ,k) )
          uvim  = 0.25d0*( u_d(im,j,k)+u_d(im,jp,k) )*( v_d(i,j,k )+v_d(im,j ,k) )
          vvjp  = 0.25d0*( v_d(i,j,k )+v_d(i,jp,k)  )*( v_d(i,j,k )+v_d(i ,jp,k) )
          vvjm  = 0.25d0*( v_d(i,j,k )+v_d(i,jm,k)  )*( v_d(i,j,k )+v_d(i ,jm,k) )
          wvkp  = 0.25d0*( w_d(i,j,k )+w_d(i,jp,k)  )*( v_d(i,j,kp)+v_d(i ,j ,k) )
          wvkm  = 0.25d0*( w_d(i,j,km)+w_d(i,jp,km) )*( v_d(i,j,km)+v_d(i ,j ,k) )
          dvdxp = (v_d(ip,j,k)-v_d(i,j,k))*dxi
          dvdxm = (v_d(i,j,k)-v_d(im,j,k))*dxi
          dvdyp = (v_d(i,jp,k)-v_d(i,j,k))*dyi
          dvdym = (v_d(i,j,k)-v_d(i,jm,k))*dyi
          dvdzp = (v_d(i,j,kp)-v_d(i,j,k))*dzci_d(k)
          dvdzm = (v_d(i,j,k)-v_d(i,j,km))*dzci_d(km)
          !
          ! Momentum balance
          !
          dvdt_d(i,j,k) = dxi*(     -uvip + uvim ) + (dvdxp-dvdxm)*visc*dxi+ &
                        dyi*(     -vvjp + vvjm ) + (dvdyp-dvdym)*visc*dyi+ &
                        dzfi_d(k)*( -wvkp + wvkm ) + (dvdzp-dvdzm)*visc*dzfi_d(k)
        enddo
      enddo
    enddo

    !istat = cudaDeviceSynchronize()
    istat = cudaStreamSynchronize(stream1)
    !$OMP END PARALLEL DO
    tauy(:) = 0.
    !$cuf kernel do(2) <<<*,*>>>
    do k=1,nz
      do j=1,ny
        dvdxp = (v_d(1 ,j,k)-v_d(0   ,j,k))*dxi*visc*dzflzi_d(k)
        dvdxm = (v_d(nx,j,k)-v_d(nx+1,j,k))*dxi*visc*dzflzi_d(k)
        tauy_d(1) = tauy_d(1) + (dvdxp+dvdxm)
      enddo
    enddo
    !$cuf kernel do(2) <<<*,*>>>
    do j=1,ny
      do i=1,nx
        dvdzp = (v_d(i,j,1 )-v_d(i,j,0   ))*dzci_d(0)*visc
        dvdzm = (v_d(i,j,nz)-v_d(i,j,nz+1))*dzci_d(nz)*visc
        tauy_d(3) = tauy_d(3) + (dvdzp+dvdzm)
      enddo
    enddo
    tauy = tauy_d
    istat = cudaDeviceSynchronize()
    
    call mpi_allreduce(MPI_IN_PLACE,tauy(1),3,MPI_REAL8,MPI_SUM,MPI_COMM_WORLD,ierr)
    nxg = nx*dims(1)
    nyg = ny*dims(2)
    nzg = nz
    tauy(1) = tauy(1)/(1.d0*nyg)
    tauy(2) = tauy(2)/(1.d0*nxg)
    tauy(3) = tauy(3)/(1.d0*nxg*nyg)
    return
  end subroutine momyad
  !
  subroutine momzad(nx,ny,nz,dxi,dyi,dzi,dzci_d,dzfi_d,dzflzi_d,visc,u_d,v_d,w_d,dwdt_d,tauz)
    implicit none
    integer, intent(in) :: nx,ny,nz
    real(8), intent(in) :: dxi,dyi,dzi,visc
    real(8), intent(in), dimension(0:), device :: dzci_d,dzfi_d
    real(8), intent(in), dimension(0:), device :: dzflzi_d
    real(8), dimension(0:,0:,0:), intent(in), device :: u_d,v_d,w_d
    real(8), dimension(:,:,:), intent(out), device :: dwdt_d
    real(8), dimension(3), intent(out) :: tauz
    real(8), dimension(3), device :: tauz_d
    integer :: im,ip,jm,jp,km,kp,i,j,k
    real(8) :: uwip,uwim,vwjp,vwjm,wwkp,wwkm
    real(8) :: dwdxp,dwdxm,dwdyp,dwdym,dwdzp,dwdzm
    integer :: nxg,nyg,nzg
    integer :: istat

    istat = cudaStreamCreate(stream2)
    !
    !$OMP PARALLEL DO DEFAULT(none) &
    !$OMP PRIVATE(i,j,k,im,jm,km,ip,jp,kp) &
    !$OMP PRIVATE(uwip,uwim,vwjp,vwjm,wwkp,wwkm) &
    !$OMP PRIVATE(dwdxp,dwdxm,dwdyp,dwdym,dwdzp,dwdzm) &
    !$OMP SHARED(nx,ny,nz,dxi,dyi,dzi,visc,u,v,w,dwdt,dzci,dzfi)
    !$cuf kernel do(3) <<<*,*,stream=stream2>>>
    do k=1,nz
      kp = k + 1
      km = k - 1
      do j=1,ny
        jp = j + 1
        jm = j - 1
        do i=1,nx
          ip = i + 1
          im = i - 1
          uwip  = 0.25d0*( w_d(i,j,k)+w_d(ip,j,k) )*( u_d(i ,j ,k)+u_d(i ,j ,kp) )
          uwim  = 0.25d0*( w_d(i,j,k)+w_d(im,j,k) )*( u_d(im,j ,k)+u_d(im,j ,kp) )
          vwjp  = 0.25d0*( w_d(i,j,k)+w_d(i,jp,k) )*( v_d(i ,j ,k)+v_d(i ,j ,kp) )
          vwjm  = 0.25d0*( w_d(i,j,k)+w_d(i,jm,k) )*( v_d(i ,jm,k)+v_d(i ,jm,kp) )
          wwkp  = 0.25d0*( w_d(i,j,k)+w_d(i,j,kp) )*( w_d(i ,j ,k)+w_d(i ,j ,kp) )
          wwkm  = 0.25d0*( w_d(i,j,k)+w_d(i,j,km) )*( w_d(i ,j ,k)+w_d(i ,j ,km) )
          dwdxp = (w_d(ip,j,k)-w_d(i,j,k))*dxi
          dwdxm = (w_d(i,j,k)-w_d(im,j,k))*dxi
          dwdyp = (w_d(i,jp,k)-w_d(i,j,k))*dyi
          dwdym = (w_d(i,j,k)-w_d(i,jm,k))*dyi
          dwdzp = (w_d(i,j,kp)-w_d(i,j,k))*dzfi_d(kp)
          dwdzm = (w_d(i,j,k)-w_d(i,j,km))*dzfi_d(k)
          !
          ! Momentum balance
          !
          dwdt_d(i,j,k) = dxi*(     -uwip + uwim ) + (dwdxp-dwdxm)*visc*dxi+ &
                        dyi*(     -vwjp + vwjm ) + (dwdyp-dwdym)*visc*dyi+ &
                        dzci_d(k)*( -wwkp + wwkm ) + (dwdzp-dwdzm)*visc*dzci_d(k)
        enddo
      enddo
    enddo
!    istat = cudaDeviceSynchronize()
    istat = cudaStreamSynchronize(stream2)
    !$OMP END PARALLEL DO
    tauz(:) = 0.
    
    !TODO: put these in overlapping streams
    !$cuf kernel do(2) <<<*,*>>>
    do k=1,nz
      do j=1,ny
        dwdxp = (w_d(1 ,j,k)-w_d(0   ,j,k))*dxi*visc*dzflzi_d(k)
        dwdxm = (w_d(nx,j,k)-w_d(nx+1,j,k))*dxi*visc*dzflzi_d(k)
        tauz_d(1) = tauz_d(1) + (dwdxp+dwdxm)
      enddo
    enddo
    !$cuf kernel do(2) <<<*,*>>>
    do k=1,nz
      do i=1,nx
        dwdyp = (w_d(i,1,k )-w_d(i,0   ,k))*dyi*visc*dzflzi_d(k)
        dwdym = (w_d(i,ny,k)-w_d(i,ny+1,k))*dyi*visc*dzflzi_d(k)
        tauz_d(2) = tauz_d(2) + (dwdyp+dwdym)
      enddo
    enddo
    tauz = tauz_d
    istat = cudaDeviceSynchronize()

    call mpi_allreduce(MPI_IN_PLACE,tauz(1),3,MPI_REAL8,MPI_SUM,MPI_COMM_WORLD,ierr)
    nxg = nx*dims(1)
    nyg = ny*dims(2)
    nzg = nz
    tauz(1) = tauz(1)/(1.d0*nyg)
    tauz(2) = tauz(2)/(1.d0*nxg)
    tauz(3) = tauz(3)/(1.d0*nxg*nyg)
    return
  end subroutine momzad
  !
  subroutine momxp(nx,ny,nz,dxi,p,dudt)
    implicit none
    integer, intent(in) :: nx,ny,nz
    real(8), intent(in) :: dxi 
    real(8), dimension(0:,0:,0:), intent(in) :: p
    real(8), dimension(:,:,:), intent(out) :: dudt
    integer :: i,j,k
    integer :: ip
    !
    !$OMP PARALLEL DO DEFAULT(none) &
    !$OMP PRIVATE(i,j,k,ip) &
    !$OMP SHARED(nx,ny,nz,dxi,p,dudt)
    do k=1,nz
      do j=1,ny
        do i=1,nx
          ip = i + 1
          dudt(i,j,k) = - dxi*( p(ip,j,k)-p(i,j,k) )
        enddo
      enddo
    enddo
    !$OMP END PARALLEL DO
    return
  end subroutine momxp
  !
  subroutine momyp(nx,ny,nz,dyi,p,dvdt)
    implicit none
    integer, intent(in) :: nx,ny,nz
    real(8), intent(in) :: dyi 
    real(8), dimension(0:,0:,0:), intent(in) :: p
    real(8), dimension(:,:,:), intent(out) :: dvdt
    integer :: i,j,k
    integer :: jp
    !
    !$OMP PARALLEL DO DEFAULT(none) &
    !$OMP PRIVATE(i,j,k,jp) &
    !$OMP SHARED(nx,ny,nz,dyi,p,dvdt)
    do k=1,nz
      do j=1,ny
        jp = j + 1
        do i=1,nx
          dvdt(i,j,k) = - dyi*( p(i,jp,k)-p(i,j,k) )
        enddo
      enddo
    enddo
    !$OMP END PARALLEL DO
    return
  end subroutine momyp
  !
  subroutine momzp(nx,ny,nz,dzci,p,dwdt)
    implicit none
    integer, intent(in) :: nx,ny,nz
    real(8), intent(in), dimension(0:) :: dzci
    real(8), dimension(0:,0:,0:), intent(in) :: p
    real(8), dimension(:,:,:), intent(out) :: dwdt
    integer :: kp
    integer :: i,j,k
    !
    !$OMP PARALLEL DO DEFAULT(none) &
    !$OMP PRIVATE(i,j,k,kp) &
    !$OMP SHARED(nx,ny,nz,p,dwdt,dzci)
    do k=1,nz
      kp = k + 1
      do j=1,ny
        do i=1,nx
          dwdt(i,j,k) = - dzci(k)*( p(i,j,kp)-p(i,j,k) )
        enddo
      enddo
    enddo
    !$OMP END PARALLEL DO
    return
  end subroutine momzp
end module mod_mom
